-------------------------------------------------------------------------------
--                                                                           --
--                                  Alice                                    --
--                                                                           --
--                                 Contact                                   --
--                                                                           --
--                                  SPEC                                     --
--                                                                           --
--                     Copyright (C) 2012-, AdaHeads K/S                     --
--                                                                           --
--  This is free software;  you can redistribute it and/or modify it         --
--  under terms of the  GNU General Public License  as published by the      --
--  Free Software  Foundation;  either version 3,  or (at your  option) any  --
--  later version. This library is distributed in the hope that it will be   --
--  useful, but WITHOUT ANY WARRANTY;  without even the implied warranty of  --
--  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.                     --
--  You should have received a copy of the GNU General Public License and    --
--  a copy of the GCC Runtime Library Exception along with this program;     --
--  see the files COPYING3 and COPYING.RUNTIME respectively.  If not, see    --
--  <http://www.gnu.org/licenses/>.                                          --
--                                                                           --
-------------------------------------------------------------------------------

with Ada.Strings.Unbounded;
with AWS.Dispatchers.Callback;
with Common;
with GNATCOLL.JSON;
with GNATCOLL.SQL.Exec;
with My_Configuration;
with Response.Cached;
with Storage;
with Yolk.Cache.Discrete_Keys;

package Contact is

   function Callback
     return AWS.Dispatchers.Callback.Handler;
   --  Return a callback for the "contact" interface.

private

   use Ada.Strings.Unbounded;
   use GNATCOLL.JSON;
   use My_Configuration;

   type Cursor is new GNATCOLL.SQL.Exec.Forward_Cursor with null record;

   type Row is
      record
         Ce_Id                   : Natural;
         Ce_Id_Column_Name       : Unbounded_String;
         Ce_Name                 : Unbounded_String;
         Ce_Name_Column_Name     : Unbounded_String;
         Is_Human                : Boolean;
         Is_Human_Column_Name    : Unbounded_String;
         Attr_JSON               : JSON_Value;
         Attr_Org_Id             : Natural;
         Attr_Org_Id_Column_Name : Unbounded_String;
         Attr_Ce_Id              : Natural;
         Attr_Ce_Id_Column_Name  : Unbounded_String;
      end record;

   procedure Bad_Ce_Id_Key
     (Response_Object :    out Response.Object;
      Message         : in     String);
   --  If the given ce_id GET parameter is bad, then return an error to the
   --  user.

   function Create_JSON
     (C : in out Cursor)
      return Common.JSON_String;
   --  Generate a JSON_String from the Row record(s) found in the Cursor.
   --  If C is empty, then return an empty JSON_String, ie. {}.

   function Element
     (C : in Cursor)
      return Row;
   --  Transforms the low level index based Cursor into the more readable Row
   --  record.

   function Get_Ce_Id_Key
     (Response_Object : in Response.Object)
      return Natural;
   --  Return the value of the ce_id request parameter. Raise
   --  GET_Parameter_Error if ce_id is not a Natural.

   function Prepared_Query
     return GNATCOLL.SQL.Exec.Prepared_Statement;
   --  Return an SQL query as a prepared statement. We keep the query in a
   --  function of its own to protect against using sub-queries by accident and
   --  to make it more readable due to local use clauses.

   function Query_Parameters
     (Response_Object : in Response.Object)
      return GNATCOLL.SQL.Exec.SQL_Parameters;
   --  Generate the SQL parameters from given request parameters.

   package Cache is new Yolk.Cache.Discrete_Keys
     (Key_Type        => Natural,
      Element_Type    => Common.JSON_String,
      Max_Element_Age => Config.Get (Cache_Max_Element_Age));
   --  Cache for the JSON_String generated by Create_JSON.

   procedure Query_To_JSON is new Storage.Generate
     (Cursor           => Cursor,
      Query            => Prepared_Query,
      To_JSON          => Create_JSON,
      Query_Parameters => Query_Parameters);
   --  Turn the data found by Query and Query_Parameters into a JSON string and
   --  if the JSON_String object is not empty then write it to cache.

   function JSON_Response is new Response.Cached.Generate_Response
     (Cache_Key_Type         => Natural,
      Bad_Request_Parameters => Bad_Ce_Id_Key,
      Get_Cache_Key          => Get_Ce_Id_Key,
      Read_From_Cache        => Cache.Read,
      Generate_Document      => Query_To_JSON,
      Write_To_Cache         => Cache.Write);
   --  Generate the AWS.Response.Data that ultimately is delivered to the user.

end Contact;
