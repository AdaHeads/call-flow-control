-------------------------------------------------------------------------------
--                                                                           --
--                                  Alice                                    --
--                                                                           --
--                            Organization_List                              --
--                                                                           --
--                                  SPEC                                     --
--                                                                           --
--                     Copyright (C) 2012-, AdaHeads K/S                     --
--                                                                           --
--  This is free software;  you can redistribute it and/or modify it         --
--  under terms of the  GNU General Public License  as published by the      --
--  Free Software  Foundation;  either version 3,  or (at your  option) any  --
--  later version. This library is distributed in the hope that it will be   --
--  useful, but WITHOUT ANY WARRANTY;  without even the implied warranty of  --
--  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.                     --
--  You should have received a copy of the GNU General Public License and    --
--  a copy of the GCC Runtime Library Exception along with this program;     --
--  see the files COPYING3 and COPYING.RUNTIME respectively.  If not, see    --
--  <http://www.gnu.org/licenses/>.                                          --
--                                                                           --
-------------------------------------------------------------------------------

with AWS.Dispatchers.Callback;
with Common;
with My_Configuration;
with Response.Cached;
with Yolk.Cache.Discrete_Keys;

package Organization_List is

   function Callback
     return AWS.Dispatchers.Callback.Handler;
   --  Return a callback for the "organization_list" interface.

private

   use My_Configuration;

   type View_Type is (Basic, Full);

   procedure Bad_List_View_Parameter
     (Response_Object :    out Response.Object;
      Message         : in     String);
   --  Return an error to the user if a Constraint_Error is raised in the
   --  Get_List_View call.

   function Get_List_View
     (Response_Object : in Response.Object)
      return View_Type;
   --  Return Basic or Full. Note that if no "view" parameter is found, the
   --  default is Basic.

   package Cache is new Yolk.Cache.Discrete_Keys
     (Key_Type        => View_Type,
      Element_Type    => Common.JSON_String,
      Max_Element_Age => Config.Get (Cache_Max_Element_Age));
   --  Cache for the JSON_String generated by Generate_Document.

   procedure Generate_Document
     (Response_Object : in out Response.Object);
   --  Add a generated JSON_String to Response_Object and set HTTP status code
   --  and whether the JSON_String is cacheable.

   function JSON_Response is new Response.Cached.Generate_Response
     (Cache_Key_Type         => View_Type,
      Bad_Request_Parameters => Bad_List_View_Parameter,
      Get_Cache_Key          => Get_List_View,
      Read_From_Cache        => Cache.Read,
      Generate_Document      => Generate_Document,
      Write_To_Cache         => Cache.Write);
   --  Generate the AWS.Response.Data that ultimately is delivered to the user.

end Organization_List;
