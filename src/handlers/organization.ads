-------------------------------------------------------------------------------
--                                                                           --
--                                  Alice                                    --
--                                                                           --
--                              Organization                                 --
--                                                                           --
--                                  SPEC                                     --
--                                                                           --
--                     Copyright (C) 2012-, AdaHeads K/S                     --
--                                                                           --
--  This is free software;  you can redistribute it and/or modify it         --
--  under terms of the  GNU General Public License  as published by the      --
--  Free Software  Foundation;  either version 3,  or (at your  option) any  --
--  later version. This library is distributed in the hope that it will be   --
--  useful, but WITHOUT ANY WARRANTY;  without even the implied warranty of  --
--  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.                     --
--  You should have received a copy of the GNU General Public License and    --
--  a copy of the GCC Runtime Library Exception along with this program;     --
--  see the files COPYING3 and COPYING.RUNTIME respectively.  If not, see    --
--  <http://www.gnu.org/licenses/>.                                          --
--                                                                           --
-------------------------------------------------------------------------------

with AWS.Dispatchers.Callback;
with Common;
with Model;
with My_Configuration;
with Response.Cached;
with Yolk.Cache.Discrete_Keys;

package Organization is

   function Callback
     return AWS.Dispatchers.Callback.Handler;
   --  Return a callback for the "organization" interface.

private

   use My_Configuration;

   procedure Bad_Org_Id
     (Response_Object :    out Response.Object;
      Message         : in     String);
   --  If the given org_id GET parameter is bad, then return an error to the
   --  user.

   function Get_Org_Id
     (Response_Object : in Response.Object)
      return Model.Organization_Identifier;
   --  Return the value of the org_id request parameter. Raise
   --  GET_Parameter_Error if org_id is not a Natural.

   package Cache is new Yolk.Cache.Discrete_Keys
     (Key_Type        => Model.Organization_Identifier,
      Element_Type    => Common.JSON_String,
      Max_Element_Age => Config.Get (Cache_Max_Element_Age));
   --  Cache for the JSON_String generated by Create_JSON.

   procedure Generate_Document
     (Response_Object : in out Response.Object);
   --  Add a generated JSON_String to Response_Object and set HTTP status code
   --  and whether the JSON_String is cacheable.

   function JSON_Response is new Response.Cached.Generate_Response
     (Cache_Key_Type         => Model.Organization_Identifier,
      Bad_Request_Parameters => Bad_Org_Id,
      Get_Cache_Key          => Get_Org_Id,
      Read_From_Cache        => Cache.Read,
      Generate_Document      => Generate_Document,
      Write_To_Cache         => Cache.Write);
   --  Generate the AWS.Response.Data that ultimately is delivered to the user.

end Organization;
